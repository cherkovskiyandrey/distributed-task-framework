plugins {
    alias(libs.plugins.spring.boot) apply(false)
    alias(libs.plugins.org.ajoberstar.grgit) apply(false)
    alias(libs.plugins.org.jreleaser) apply(false)
    id "base"
}

var grgitPluginId = libs.plugins.org.ajoberstar.grgit.get().pluginId
var jreleaserPluginId = libs.plugins.org.jreleaser.get().pluginId

description = "The task processing framework with saga supporting"

var libraries = subprojects.findAll { !it.name.contains('application') }
var apps = subprojects.findAll { it.name.contains('application') }

allprojects {
    group = 'io.github.cherkovskiyandrey'
    version = project.properties['version']
    version = version == 'unspecified' ? '2.0.0' : version

    apply plugin: libs.plugins.spring.boot.get().pluginId
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'base'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        //dependency management
        implementation platform(libs.spring.boot.bom)

        //Lombok
        compileOnly libs.org.projectlombok.lombok
        annotationProcessor libs.org.projectlombok.lombok
        testCompileOnly libs.org.projectlombok.lombok
        testAnnotationProcessor libs.org.projectlombok.lombok

        //Mapstruct
        implementation libs.org.mapstruct
        annotationProcessor libs.org.mapstruct.processor
        annotationProcessor libs.org.lombok.mapstruct.binding

        //Common tools
        implementation libs.jackson.databind
        implementation libs.guava
        implementation libs.commons.lang3
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
//        withJavadocJar()
        withSourcesJar()
    }

    jar {
        enabled = false
    }

    bootJar {
        enabled = false
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

configure(libraries) { project_ ->

    apply plugin: grgitPluginId
    apply plugin: jreleaserPluginId

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    test {
        maxHeapSize = "2048m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    publishing {
        repositories {
            maven {
                setUrl(layout.buildDirectory.dir("staging-deploy"))
            }
            mavenLocal()
        }
        publications {
            maven(MavenPublication) {
                from components.java

                var currentTag = grgit.tag.list().find {
                    it.commit.id == grgit.head().getId()
                }
                if (currentTag != null) {
                    var ver = currentTag.name
                    if (ver.startsWith("v")) {
                        ver = ver.substring(1)
                    }
                    version = ver
                } else {
                    version = System.getProperty("CI_COMMIT_BRANCH", grgit.getBranch().current().getName()).replaceAll("\\W", "_") +
                            "-" +
                            new Date().format('yyyyMMdd_HHmmss') +
                            "-" +
                            grgit.head().getId().substring(0, 8)
                }

                groupId = "io.github.cherkovskiyandrey"

                pom {
                    name = "Distributed Task Framework"
                    description = "The task processing framework"
                    inceptionYear = '2022'
                    url = "https://github.com/cherkovskiyandrey/distributed-task-framework"
                    issueManagement {
                        url = "https://github.com/cherkovskiyandrey/distributed-task-framework/issues"
                    }

                    scm {
                        url = "https://github.com/cherkovskiyandrey/distributed-task-framework"
                        connection = "scm:git://github.com/cherkovskiyandrey/distributed-task-framework.git"
                        developerConnection = "scm:git://github.com/cherkovskiyandrey/distributed-task-framework.git"
                    }

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "cherkovskiyandrey"
                            name = "Andrey Cherkovskiy"
                            email = "andreycherkovskiy@gmail.com"
                            url = "https://github.com/cherkovskiyandrey"
                        }
                    }
                }
            }
        }
    }

    // JReleaser configuration only in root project
    jreleaser {
        dryrun = false
        project {
            authors = ['Andrey Cherkovskiy']
            license = 'Apache-2.0'
            links {
                homepage = 'https://github.com/cherkovskiyandrey/distributed-task-framework'
            }
            inceptionYear = '2022'
        }
        release {
            github {
                skipRelease = true
                skipTag = true
                sign = true
                branch = "main"
                branchPush = "main"
                overwrite = true
            }
        }
        signing {
            active = 'ALWAYS'
            armored = true
        }
        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = "https://central.sonatype.com/api/v1/publisher"
                        stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                        setAuthorization("Basic")
                        retryDelay = 60
                    }
                }
            }
        }
    }
}

configure(apps) { project ->
    bootJar {
        enabled = true
    }

    jar {
        enabled = true
    }
}

publish {
    dependsOn libraries.publish
}

build {
    dependsOn subprojects.build
}
