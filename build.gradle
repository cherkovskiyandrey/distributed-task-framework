import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jreleaser' version '1.17.0'
    id "base"
    id 'org.ajoberstar.grgit' version "5.2.2"
}

description = "The task processing framework"

var libs = subprojects.findAll { !it.name.contains('application') }
var apps = subprojects.findAll { it.name.contains('application') }

configure(subprojects) { project ->
    group = 'io.github.cherkovskiyandrey'
    version = '1.0.0'

    ext {
        lombokVersion = '1.18.30'
        mapstructVersion = '1.5.5.Final'
        lombokMapstructBindingVersion = '0.2.0'
        springDataJdbcVersion = '3.1.2'
        guavaVersion = '31.0.1-jre'
        testcontainersVersion = '1.19.6'
        cronUtilsVersion = '9.1.6'
        caffeineVersion = '3.0.5'
        documenterPluginVersion = '0.7.1'
    }

    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'base'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"

        implementation "org.mapstruct:mapstruct:${mapstructVersion}"
        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation "com.google.guava:guava:${guavaVersion}"
        implementation 'org.apache.commons:commons-lang3'

        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"

        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
//        withJavadocJar()
        withSourcesJar()
    }
}

configure(libs) { project_ ->

    apply plugin: 'org.ajoberstar.grgit'

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    build {
        finalizedBy publishToMavenLocal
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        maxHeapSize = "2048m"
        testLogging {
            exceptionFormat = 'full'
        }
    }

    publishing {
        repositories {
            maven {
                setUrl(layout.buildDirectory.dir("staging-deploy"))
            }
            mavenLocal()
        }
        publications {
            maven(MavenPublication) {
                from components.java

                var currentTag = grgit.tag.list().find {
                    it.commit.id == grgit.head().getId()
                }
                if (currentTag != null) {
                    var ver = currentTag.name
                    if (ver.startsWith("v")) {
                        ver = ver.substring(1)
                    }
                    version = ver
                } else {
                    version = System.getProperty("CI_COMMIT_BRANCH", grgit.getBranch().current().getName()).replaceAll("\\W", "_") +
                            "-" +
                            new Date().format('yyyyMMdd_HHmmss') +
                            "-" +
                            grgit.head().getId().substring(0, 8)
                }

                groupId = "io.github.cherkovskiyandrey"

                pom {
                    name = "Distributed Task Framework"
                    description = "The task processing framework"
                    inceptionYear = '2022'
                    url = "https://github.com/cherkovskiyandrey/distributed-task-framework"
                    issueManagement {
                        url = "https://github.com/cherkovskiyandrey/distributed-task-framework/issues"
                    }

                    scm {
                        url = "https://github.com/cherkovskiyandrey/distributed-task-framework"
                        connection ="scm:git://github.com/cherkovskiyandrey/distributed-task-framework.git"
                        developerConnection = "scm:git://github.com/cherkovskiyandrey/distributed-task-framework.git"
                    }

                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "cherkovskiyandrey"
                            name = "Andrey Cherkovskiy"
                            email = "andreycherkovskiy@gmail.com"
                            url = "https://github.com/cherkovskiyandrey"
                        }
                    }
                }
            }
        }
    }

    jreleaser {
        dryrun = false
        //noinspection GroovyAssignabilityCheck
        project {
            authors = ['Andrey Cherkovskiy']
            license = 'Apache-2.0'
            //noinspection GroovyAssignabilityCheck
            links {
                homepage = 'https://github.com/cherkovskiyandrey/distributed-task-framework'
            }
            inceptionYear = '2022'
        }
        release {
            //noinspection GroovyAssignabilityCheck
            github {
                skipRelease = true
                skipTag = true
                sign = true
                branch = "main"
                branchPush = "main"
                overwrite = true
            }
        }
        //noinspection GroovyAssignabilityCheck
        signing {
            active = 'ALWAYS'
            armored = true
            verify = true
        }
        deploy {
            maven {
                //noinspection GroovyAssignabilityCheck
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = "https://central.sonatype.com/api/v1/publisher"
                        stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                        setAuthorization("Basic")
                        retryDelay = 60
                    }
                }
            }
        }
    }

}

configure(apps) { project ->
    bootJar {
        enabled = true
    }

    jar {
        enabled = true
    }
}
